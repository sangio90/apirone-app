<cfcomponent extends="com.apirone.core.model.dao.AbsDAO" accessors="true">

	<!--- ======================= Persone ======================= --->

	<!---
		Utenti con tipo di lavoro e ore disponibili ( pagina Disponibilità persone ).
		onlyWithType: solo quelli che si pianificano ( Gantt ).
	--->
	<cffunction name="findPeople" returntype="Query" access="public">
		<cfargument name="onlyWithType" type="Boolean" default="false">

		<cfquery name="local.q" datasource="apirone">
			SELECT
				u.user_id::varchar AS user_id,
				COALESCE( NULLIF( u.user_name, '' ), a.account ) AS name,
				u.role_id,
				u.status_id,
				u.work_type_id,
				u.hours_mon, u.hours_tue, u.hours_wed, u.hours_thu, u.hours_fri, u.hours_sat, u.hours_sun,
				EXTRACT( EPOCH FROM u.day_start )::int / 60 AS day_start_minutes,
				cur.hourly_cost,
				cur.valid_from AS cost_valid_from
			FROM membership.users u
			JOIN membership.accounts a ON a.account_id = u.account_id
			<!--- tariffa in vigore oggi --->
			LEFT JOIN LATERAL (
				SELECT c.hourly_cost, c.valid_from
				FROM planning_person_costs c
				WHERE c.user_id = u.user_id AND c.valid_from <= CURRENT_DATE
				ORDER BY c.valid_from DESC
				LIMIT 1
			) cur ON true
			WHERE 1=1
				<cfif arguments.onlyWithType>
					AND u.work_type_id IS NOT NULL
				</cfif>
			ORDER BY 2
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<cffunction name="updatePerson" returntype="void" access="public">
		<cfargument name="userId" type="String" required="true">
		<cfargument name="workTypeId" type="String" required="true">
		<cfargument name="hours" type="Array" required="true">
		<!--- ora di inizio giornata "HH:MM" --->
		<cfargument name="dayStart" type="String" required="true">

		<cfquery datasource="apirone">
			UPDATE membership.users SET
				day_start = <cfqueryparam cfsqltype="varchar" value="#arguments.dayStart#">::time,
				work_type_id = <cfqueryparam cfsqltype="varchar" value="#arguments.workTypeId#" null="#!Len( arguments.workTypeId )#">,
				hours_mon = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 1 ]#">,
				hours_tue = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 2 ]#">,
				hours_wed = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 3 ]#">,
				hours_thu = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 4 ]#">,
				hours_fri = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 5 ]#">,
				hours_sat = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 6 ]#">,
				hours_sun = <cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours[ 7 ]#">
			WHERE user_id = <cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid
		</cfquery>
	</cffunction>

	<!--- ======================= Ore dei preventivi ======================= --->

	<!--- budget: stringa vuota = nessun budget --->
	<cffunction name="saveQuotationWorkHours" returntype="void" access="public">
		<cfargument name="quotationId" type="String" required="true">
		<cfargument name="workTypeId" type="String" required="true">
		<cfargument name="hours" type="Numeric" required="true">
		<cfargument name="budget" type="String" default="">

		<cfquery datasource="apirone">
			INSERT INTO quotation_work_hours ( quotation_id, work_type_id, hours, budget )
			VALUES (
				<cfqueryparam cfsqltype="varchar" value="#arguments.quotationId#">::uuid,
				<cfqueryparam cfsqltype="varchar" value="#arguments.workTypeId#">,
				<cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hours#">,
				<cfqueryparam cfsqltype="numeric" scale="2" value="#Val( arguments.budget )#" null="#!IsNumeric( arguments.budget )#">
			)
			ON CONFLICT ( quotation_id, work_type_id ) DO UPDATE
				SET hours = EXCLUDED.hours, budget = EXCLUDED.budget, updated_at = NOW()
		</cfquery>
	</cffunction>

	<!--- ======================= Costi orari ======================= --->

	<cffunction name="findPersonCosts" returntype="Query" access="public">
		<cfargument name="userId" type="String" required="true">

		<cfquery name="local.q" datasource="apirone">
			SELECT cost_id, valid_from, hourly_cost
			FROM planning_person_costs
			WHERE user_id = <cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid
			ORDER BY valid_from DESC
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<cffunction name="savePersonCost" returntype="void" access="public">
		<cfargument name="userId" type="String" required="true">
		<cfargument name="validFrom" type="Date" required="true">
		<cfargument name="hourlyCost" type="Numeric" required="true">
		<cfargument name="createdBy" type="String" default="">

		<cfquery datasource="apirone">
			INSERT INTO planning_person_costs ( user_id, valid_from, hourly_cost, created_by )
			VALUES (
				<cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid,
				<cfqueryparam cfsqltype="date" value="#arguments.validFrom#">,
				<cfqueryparam cfsqltype="numeric" scale="2" value="#arguments.hourlyCost#">,
				<cfqueryparam cfsqltype="varchar" value="#arguments.createdBy#" null="#!Len( arguments.createdBy )#">::uuid
			)
			ON CONFLICT ( user_id, valid_from ) DO UPDATE SET hourly_cost = EXCLUDED.hourly_cost
		</cfquery>
	</cffunction>

	<cffunction name="deletePersonCost" returntype="void" access="public">
		<cfargument name="costId" type="Numeric" required="true">

		<cfquery datasource="apirone">
			DELETE FROM planning_person_costs
			WHERE cost_id = <cfqueryparam cfsqltype="integer" value="#arguments.costId#">
		</cfquery>
	</cffunction>

	<!---
		Costi dei progetti per tipo di attività: ore pianificate × tariffa della
		persona in vigore quel giorno; "done" = giorni fino a oggi; missing_rate_hours
		= ore di persone senza tariffa a quella data ( costo non calcolabile ).
	--->
	<cffunction name="findCostStats" returntype="Query" access="public">
		<cfquery name="local.q" datasource="apirone">
			SELECT
				b.quotation_id::varchar AS quotation_id,
				b.work_type_id,
				SUM( d.hours ) AS hours,
				SUM( CASE WHEN d.day <= CURRENT_DATE THEN d.hours ELSE 0 END ) AS done_hours,
				SUM( d.hours * COALESCE( r.hourly_cost, 0 ) ) AS cost,
				SUM( CASE WHEN d.day <= CURRENT_DATE THEN d.hours * COALESCE( r.hourly_cost, 0 ) ELSE 0 END ) AS done_cost,
				SUM( CASE WHEN r.hourly_cost IS NULL THEN d.hours ELSE 0 END ) AS missing_rate_hours,
				SUM( CASE WHEN b.extra THEN d.hours ELSE 0 END ) AS extra_hours,
				SUM( CASE WHEN b.extra THEN d.hours * COALESCE( r.hourly_cost, 0 ) ELSE 0 END ) AS extra_cost
			FROM planning_blocks b
			JOIN planning_block_days d ON d.block_id = b.block_id
			LEFT JOIN LATERAL (
				SELECT c.hourly_cost
				FROM planning_person_costs c
				WHERE c.user_id = b.user_id AND c.valid_from <= d.day
				ORDER BY c.valid_from DESC
				LIMIT 1
			) r ON true
			GROUP BY b.quotation_id, b.work_type_id
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<!--- Progetto concluso nel Gantt: non compare più tra quelli da pianificare --->
	<cffunction name="setCompleted" returntype="void" access="public">
		<cfargument name="quotationId" type="String" required="true">
		<cfargument name="completed" type="Boolean" required="true">
		<cfargument name="userId" type="String" default="">

		<cfquery datasource="apirone">
			INSERT INTO planning_quotations ( quotation_id, completed_at, completed_by )
			VALUES (
				<cfqueryparam cfsqltype="varchar" value="#arguments.quotationId#">::uuid,
				<cfif arguments.completed>NOW()<cfelse>NULL</cfif>,
				<cfqueryparam cfsqltype="varchar" value="#arguments.userId#" null="#!arguments.completed OR !Len( arguments.userId )#">::uuid
			)
			ON CONFLICT ( quotation_id ) DO UPDATE
				SET completed_at = EXCLUDED.completed_at, completed_by = EXCLUDED.completed_by
		</cfquery>
	</cffunction>

	<!--- Revisione / duplica: le ore stimate passano al nuovo preventivo --->
	<cffunction name="copyQuotationWorkHours" returntype="void" access="public">
		<cfargument name="fromQuotationId" type="String" required="true">
		<cfargument name="toQuotationId" type="String" required="true">

		<cfquery datasource="apirone">
			INSERT INTO quotation_work_hours ( quotation_id, work_type_id, hours, budget )
			SELECT <cfqueryparam cfsqltype="varchar" value="#arguments.toQuotationId#">::uuid, work_type_id, hours, budget
			FROM quotation_work_hours
			WHERE quotation_id = <cfqueryparam cfsqltype="varchar" value="#arguments.fromQuotationId#">::uuid
			ON CONFLICT ( quotation_id, work_type_id ) DO NOTHING
		</cfquery>
	</cffunction>

	<!--- ======================= Gantt ======================= --->

	<!---
		Preventivi pianificabili: stato corrente "Confermato da cliente" o
		"Convertito in ordine", qualunque versione. Anche quelli che hanno già
		blocchi nel Gantt e nel frattempo hanno cambiato stato ( plannable false ).
	--->
	<cffunction name="findPlannableQuotations" returntype="Query" access="public">
		<cfquery name="local.q" datasource="apirone">
			SELECT
				q.quotation_id::varchar AS quotation_id,
				q.quotation_number,
				q.version_number,
				q.quotation AS name,
				q.rif_libero,
				h.status_id,
				( h.status_id IN ( 'CCN', 'CON' ) ) AS plannable,
				( pq.completed_at IS NOT NULL ) AS completed,
				ca.name AS customer
			FROM quotations q
			LEFT JOIN quotation_status_history h ON h.quotation_status_history_id = q.quotation_status_history_id
			LEFT JOIN planning_quotations pq ON pq.quotation_id = q.quotation_id
			LEFT JOIN crm_accounts ca ON ca.id = q.customer_id::varchar
			WHERE h.status_id IN ( 'CCN', 'CON' )
				OR q.quotation_id IN ( SELECT quotation_id FROM planning_blocks )
			ORDER BY q.quotation_number, q.version_number
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<!--- Ore stimate e pianificate per preventivo e tipo ( preventivi pianificabili ) --->
	<cffunction name="findPlannableQuotationHours" returntype="Query" access="public">
		<cfquery name="local.q" datasource="apirone">
			WITH plannable AS (
				SELECT q.quotation_id
				FROM quotations q
				JOIN quotation_status_history h ON h.quotation_status_history_id = q.quotation_status_history_id
				WHERE h.status_id IN ( 'CCN', 'CON' )
				UNION
				SELECT quotation_id FROM planning_blocks
			),
			<!--- le ore extra non consumano la stima: a parte --->
			planned AS (
				SELECT
					b.quotation_id,
					b.work_type_id,
					SUM( CASE WHEN b.extra THEN 0 ELSE d.hours END ) AS hours,
					SUM( CASE WHEN b.extra THEN d.hours ELSE 0 END ) AS extra_hours
				FROM planning_blocks b
				JOIN planning_block_days d ON d.block_id = b.block_id
				WHERE b.quotation_id IN ( SELECT quotation_id FROM plannable )
				GROUP BY b.quotation_id, b.work_type_id
			)
			SELECT
				COALESCE( e.quotation_id, p.quotation_id )::varchar AS quotation_id,
				COALESCE( e.work_type_id, p.work_type_id ) AS work_type_id,
				COALESCE( e.hours, 0 ) AS estimated,
				e.budget,
				COALESCE( p.hours, 0 ) AS planned,
				COALESCE( p.extra_hours, 0 ) AS extra
			FROM ( SELECT * FROM quotation_work_hours WHERE quotation_id IN ( SELECT quotation_id FROM plannable ) ) e
			FULL OUTER JOIN planned p ON p.quotation_id = e.quotation_id AND p.work_type_id = e.work_type_id
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<!---
		Blocchi con almeno un giorno nell'intervallo, con tutti i loro giorni
		( servono inizio, fine e totale del blocco anche fuori dall'intervallo ).
	--->
	<cffunction name="findBlocks" returntype="Query" access="public">
		<cfargument name="fromDate" type="Date" required="true">
		<cfargument name="toDate" type="Date" required="true">

		<cfquery name="local.q" datasource="apirone">
			SELECT
				b.block_id,
				b.quotation_id::varchar AS quotation_id,
				b.user_id::varchar AS user_id,
				b.work_type_id,
				b.use_saturday,
				b.force_holidays,
				b.note,
				b.extra,
				d.day,
				d.hours,
				<!--- ora di inizio nel giorno, in minuti; senza: inizio giornata della persona --->
				EXTRACT( EPOCH FROM COALESCE( d.start_time, u.day_start ) )::int / 60 AS start_minutes
			FROM planning_blocks b
			JOIN planning_block_days d ON d.block_id = b.block_id
			JOIN membership.users u ON u.user_id = b.user_id
			WHERE b.block_id IN (
				SELECT block_id FROM planning_block_days
				WHERE day BETWEEN <cfqueryparam cfsqltype="date" value="#arguments.fromDate#">
					AND <cfqueryparam cfsqltype="date" value="#arguments.toDate#">
			)
			ORDER BY b.block_id, d.day
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<!--- ======================= Blocchi ======================= --->

	<cffunction name="readPerson" returntype="Query" access="public">
		<cfargument name="userId" type="String" required="true">

		<cfquery name="local.q" datasource="apirone">
			SELECT
				user_id::varchar AS user_id,
				work_type_id,
				hours_mon, hours_tue, hours_wed, hours_thu, hours_fri, hours_sat, hours_sun,
				EXTRACT( EPOCH FROM day_start )::int / 60 AS day_start_minutes
			FROM membership.users
			WHERE user_id = <cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<cffunction name="readBlock" returntype="Query" access="public">
		<cfargument name="blockId" type="Numeric" required="true">

		<cfquery name="local.q" datasource="apirone">
			SELECT
				b.block_id,
				b.quotation_id::varchar AS quotation_id,
				b.user_id::varchar AS user_id,
				b.work_type_id,
				b.use_saturday,
				b.force_holidays,
				b.note,
				b.extra,
				b.start_day,
				d.day,
				d.hours,
				EXTRACT( EPOCH FROM COALESCE( d.start_time, u.day_start ) )::int / 60 AS start_minutes
			FROM planning_blocks b
			JOIN membership.users u ON u.user_id = b.user_id
			LEFT JOIN planning_block_days d ON d.block_id = b.block_id
			WHERE b.block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
			ORDER BY d.day
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<!---
		Pezzi di giornata già pianificati per una persona ( escluso un blocco ):
		giorno, inizio in minuti, ore.
	--->
	<cffunction name="findBookedSegments" returntype="Query" access="public">
		<cfargument name="userId" type="String" required="true">
		<cfargument name="fromDate" type="Date" required="true">
		<cfargument name="toDate" type="Date" required="true">
		<cfargument name="excludeBlockId" type="Numeric" default="0">

		<cfquery name="local.q" datasource="apirone">
			SELECT
				d.day,
				d.hours,
				EXTRACT( EPOCH FROM COALESCE( d.start_time, u.day_start ) )::int / 60 AS start_minutes
			FROM planning_block_days d
			JOIN planning_blocks b ON b.block_id = d.block_id
			JOIN membership.users u ON u.user_id = b.user_id
			WHERE b.user_id = <cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid
				AND d.day BETWEEN <cfqueryparam cfsqltype="date" value="#arguments.fromDate#">
					AND <cfqueryparam cfsqltype="date" value="#arguments.toDate#">
				AND b.block_id <> <cfqueryparam cfsqltype="integer" value="#arguments.excludeBlockId#">
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<cffunction name="insertBlock" returntype="Numeric" access="public">
		<cfargument name="quotationId" type="String" required="true">
		<cfargument name="userId" type="String" required="true">
		<cfargument name="workTypeId" type="String" required="true">
		<cfargument name="useSaturday" type="Boolean" default="false">
		<cfargument name="forceHolidays" type="Boolean" default="false">
		<cfargument name="startDay" type="String" default="">
		<cfargument name="note" type="String" default="">
		<cfargument name="extra" type="Boolean" default="false">
		<cfargument name="createdBy" type="String" default="">

		<cfquery name="local.q" datasource="apirone">
			INSERT INTO planning_blocks ( quotation_id, user_id, work_type_id, use_saturday, force_holidays, start_day, note, extra, created_by )
			VALUES (
				<cfqueryparam cfsqltype="varchar" value="#arguments.quotationId#">::uuid,
				<cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid,
				<cfqueryparam cfsqltype="varchar" value="#arguments.workTypeId#">,
				<cfqueryparam cfsqltype="boolean" value="#arguments.useSaturday#">,
				<cfqueryparam cfsqltype="boolean" value="#arguments.forceHolidays#">,
				<cfqueryparam cfsqltype="date" value="#arguments.startDay#" null="#!IsDate( arguments.startDay )#">,
				<cfqueryparam cfsqltype="varchar" value="#arguments.note#" null="#!Len( Trim( arguments.note ) )#">,
				<cfqueryparam cfsqltype="boolean" value="#arguments.extra#">,
				<cfqueryparam cfsqltype="varchar" value="#arguments.createdBy#" null="#!Len( arguments.createdBy )#">::uuid
			)
			RETURNING block_id
		</cfquery>

		<cfreturn local.q.block_id>
	</cffunction>

	<cffunction name="updateBlock" returntype="void" access="public">
		<cfargument name="blockId" type="Numeric" required="true">
		<cfargument name="userId" type="String" required="true">
		<cfargument name="useSaturday" type="Boolean" required="true">
		<cfargument name="forceHolidays" type="Boolean" required="true">
		<cfargument name="startDay" type="String" required="true">

		<cfquery datasource="apirone">
			UPDATE planning_blocks SET
				user_id = <cfqueryparam cfsqltype="varchar" value="#arguments.userId#">::uuid,
				use_saturday = <cfqueryparam cfsqltype="boolean" value="#arguments.useSaturday#">,
				force_holidays = <cfqueryparam cfsqltype="boolean" value="#arguments.forceHolidays#">,
				start_day = <cfqueryparam cfsqltype="date" value="#arguments.startDay#" null="#!IsDate( arguments.startDay )#">
			WHERE block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
		</cfquery>
	</cffunction>

	<cffunction name="updateBlockExtra" returntype="void" access="public">
		<cfargument name="blockId" type="Numeric" required="true">
		<cfargument name="extra" type="Boolean" required="true">

		<cfquery datasource="apirone">
			UPDATE planning_blocks
			SET extra = <cfqueryparam cfsqltype="boolean" value="#arguments.extra#">
			WHERE block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
		</cfquery>
	</cffunction>

	<cffunction name="updateBlockNote" returntype="void" access="public">
		<cfargument name="blockId" type="Numeric" required="true">
		<cfargument name="note" type="String" required="true">

		<cfquery datasource="apirone">
			UPDATE planning_blocks
			SET note = <cfqueryparam cfsqltype="varchar" value="#Trim( arguments.note )#" null="#!Len( Trim( arguments.note ) )#">
			WHERE block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
		</cfquery>
	</cffunction>

	<!--- Sostituisce i giorni del blocco: days [ { day ( yyyy-mm-dd ), hours, start ( minuti, facoltativo ) } ] --->
	<cffunction name="replaceBlockDays" returntype="void" access="public">
		<cfargument name="blockId" type="Numeric" required="true">
		<cfargument name="days" type="Array" required="true">

		<cfquery datasource="apirone">
			DELETE FROM planning_block_days
			WHERE block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
		</cfquery>

		<cfloop array="#arguments.days#" item="local.day">
			<cfquery datasource="apirone">
				<cfset local.start = local.day.start ?: "">
				INSERT INTO planning_block_days ( block_id, day, hours, start_time )
				VALUES (
					<cfqueryparam cfsqltype="integer" value="#arguments.blockId#">,
					<cfqueryparam cfsqltype="date" value="#local.day.day#">,
					<cfqueryparam cfsqltype="numeric" scale="2" value="#local.day.hours#">,
					<cfif IsNumeric( local.start )>
						make_time( <cfqueryparam cfsqltype="integer" value="#Int( local.start / 60 )#">, <cfqueryparam cfsqltype="integer" value="#local.start mod 60#">, 0 )
					<cfelse>
						NULL
					</cfif>
				)
			</cfquery>
		</cfloop>
	</cffunction>

	<cffunction name="deleteBlock" returntype="void" access="public">
		<cfargument name="blockId" type="Numeric" required="true">

		<cfquery datasource="apirone">
			DELETE FROM planning_blocks
			WHERE block_id = <cfqueryparam cfsqltype="integer" value="#arguments.blockId#">
		</cfquery>
	</cffunction>

	<!--- ======================= Chiusure aziendali ======================= --->

	<cffunction name="findClosures" returntype="Query" access="public">
		<cfargument name="fromDate" type="Date" required="true">
		<cfargument name="toDate" type="Date" required="true">

		<cfquery name="local.q" datasource="apirone">
			SELECT closure_id, day, description
			FROM planning_closures
			WHERE day BETWEEN <cfqueryparam cfsqltype="date" value="#arguments.fromDate#">
				AND <cfqueryparam cfsqltype="date" value="#arguments.toDate#">
			ORDER BY day
		</cfquery>

		<cfreturn local.q>
	</cffunction>

	<cffunction name="saveClosure" returntype="void" access="public">
		<cfargument name="day" type="Date" required="true">
		<cfargument name="description" type="String" default="">

		<cfquery datasource="apirone">
			INSERT INTO planning_closures ( day, description )
			VALUES (
				<cfqueryparam cfsqltype="date" value="#arguments.day#">,
				<cfqueryparam cfsqltype="varchar" value="#arguments.description#">
			)
			ON CONFLICT ( day ) DO UPDATE SET description = EXCLUDED.description
		</cfquery>
	</cffunction>

	<cffunction name="deleteClosure" returntype="void" access="public">
		<cfargument name="closureId" type="Numeric" required="true">

		<cfquery datasource="apirone">
			DELETE FROM planning_closures
			WHERE closure_id = <cfqueryparam cfsqltype="integer" value="#arguments.closureId#">
		</cfquery>
	</cffunction>

</cfcomponent>
